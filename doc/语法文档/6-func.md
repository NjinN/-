
---
### 函数

RML中函数的定义方式如下

```
f: func [x y] [x + y]
```
这里的 `func` 是RML内置的原生函数，用于创建用户自定义函数。`func`函数需要两个方块作为参数，第一个方块是自定义函数的参数列表，第二个方块是自定义函数的执行体。
</br>
上面自定义的函数绑定到了单字 `f`，可以通过以下方式调用

```
f 1 2			;返回 3
```
当解释器载入 `f` 时会去查找其对应的值，当发现其绑定的是一个函数时，则开启新的表达式，并接受其后的Token作为实参。自定义函数会新建一个函数语境，父语境为调用时的即时语境。然后根据函数定义的形参列表，在函数语境中将实参与形参名绑定，之后在函数语境中执行函数的代码块。
</br>
函数默认返回执行体的最后一个表达式的运行结果。也可以通过 `return` 指定返回值。

```
f: func [] [1 return 2 3] 
f 		;返回 2
```
</br>
RML中可通过修饰字拓展函数的功能，例如

```
f: func [n /x] [
	if x [return x]
	return n
]

f 5			;返回 5
f/x 5		;返回 true
```
这里我们给函数 `f` 定义了一个形参 `n` 和一个修饰字 `/x` </br>
当我们在调用函数时带上修饰字如 `f/x` ，就会在函数语境中给修饰字对应的变量名绑定一个 `true`值，如果没有带上该修饰字，则绑定一个 `false` 值。因此，示例中 `f` 返回了形参 `n` 对应的值5，而 `f/x` 则返回了修饰字对应变量`x`所绑定的 `true`
</br>
</br>
我们还可以让修饰字接受参数，例如

```
f: func [n /x x] [
	if x [return x]
	return n
]

f 5			;返回 5
f/x 5 10	;返回 10		

```
当定义修饰字时，在修饰字后加上一个单字时，修饰字就能接收一个参数，并在执行时绑定到其后的单字上。
</br>
<b style="color:red;">注意</b>，若有多个带参修饰字同时使用时，会按带参修饰字出现的顺序依次获取实参。
</br></br>
函数有专门的字面表达形式，也可以直接通过字面值定义函数，例如

```
f: !func{[x y] [x + y]}
```
</br>
定义函数时可以对函数和参数加上文档说明。当参数列表的方块中，第一个Token时字符串类型时，视为对整个函数的说明。在定义每一个形参后修饰字后加上一个字符串，则视为对该行参或修饰字的说明。例如

```
f: func [
	"这是函数说明"
	n		"这是参数说明"
	/a		"这是一个无参的修饰字"
	/b b	"这是一个有参的修饰字"
] [
	return n
]

```
可以通过 `help` 或 `?` 函数查看函数的文档，例如

```
? f
```
将得到以下输出

```
FUNC:
  desc:    这是函数说明

  args:    n            这是参数说明

  props:   /a   		这是一个无参的修饰字
           /b  b        这是一个有参的修饰字


```


















